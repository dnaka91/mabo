check:
  #!/usr/bin/env fish

  echo src/**/*.mabo | xargs -n 1 -P 8 -- just check-mabo
  echo src/**/*.rs   | xargs -n 1 -P 8 -- just check-rust
  echo src/**/*.go   | xargs -n 1 -P 8 -- just check-go
  echo src/**/*.kt   | xargs -n 1 -P 8 -- just check-kotlin
  echo src/**/*.ts   | xargs -n 1 -P 8 -- just check-typescript
  echo src/**/*.py   | xargs -n 1 -P 8 -- just check-python

@check-mabo file:
  echo checking {{file}}
  cargo run --quiet --package mabo-cli -- check {{file}}

@check-rust file:
  echo checking {{file}}
  rustc --crate-type lib --allow dead_code --out-dir $(mktemp -d) {{file}}

@check-go file:
  echo checking {{file}}
  go build -o $(mktemp) {{file}}

@check-kotlin file:
  echo checking {{file}}
  kotlinc -d $(mktemp -d) {{file}}

@check-typescript file:
  echo checking {{file}}
  tsc --strict --target es2020 --outDir $(mktemp -d) {{file}}

@check-python file:
  echo checking {{file}}
  mypy --strict --no-error-summary --cache-dir /tmp {{file}}

format:
  #!/usr/bin/env fish

  just format-mabo       src/**/*.mabo
  just format-rust       src/**/*.rs
  just format-go         src/**/*.go
  just format-kotlin     src/**/*.kt
  just format-typescript src/**/*.ts
  just format-python     src/**/*.py

@format-mabo +files:
  cargo run --quiet --package mabo-cli -- format {{files}}

@format-rust +files:
  rustup run nightly rustfmt --edition 2021 {{files}}

@format-go +files:
  gofmt -w {{files}}

@format-kotlin +files:
  ktlint -F {{files}}

@format-typescript +files:
  prettier --log-level warn -w {{files}}

@format-python +files:
  black -q {{files}}

dev:
  bun run dev

build:
  bun run build
